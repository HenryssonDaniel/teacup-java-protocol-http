plugins {
    id 'java-library'
    id 'maven'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.henryssondaniel.teacup.protocol'
version = '1.0.0-SNAPSHOT'

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

dependencies {
    api 'io.github.henryssondaniel.teacup:core:1.0.0-SNAPSHOT'
    testImplementation 'org.assertj:assertj-core:3.12.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.1'
    testImplementation 'org.mockito:mockito-core:2.25.1'
}

publishing {
    publications {
        http(MavenPublication) {
            from components.java
        }
    }
}

repositories {
    jcenter()
    mavenLocal()
}

signing {
    sign configurations.archives
}

test {
    useJUnitPlatform {}

    testLogging {
        showStandardStreams = true
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name project.group + ':' + project.name
                description 'Java Testing Framework for Communication Protocols and Web Services with HTTP'
                url 'https://github.com/HenryssonDaniel/teacup-java-protocol-http'

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        name 'Daniel Henrysson'
                        email 'henrysson.daniel@gmail.com'
                        organization 'Daniel Henrysson AB'
                        organizationUrl 'https://github.com/HenryssonDaniel'
                    }
                }

                scm {
                    connection 'scm:git:git://github.com/HenryssonDaniel/teacup-java-protocol-http.git'
                    developerConnection 'scm:git:ssh://github.com/HenryssonDaniel/teacup-java-protocol-http.git'
                    url 'https://github.com/HenryssonDaniel/teacup-java-protocol-http'
                }
            }
        }
    }
}